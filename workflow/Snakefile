
# main config file
configfile: "config/config.yml"

# add all samples and optional cell numbers to snakemake config object
include: "scripts/add_samples_to_config.py"

# process data for all samples
rule all:
  input:
    expand("results/{sample}/{file}", sample = config["samples"],
           file = ["Seurat.rds", "Pipeline_Report.html"])

# create the yaml file specifying all input and pipeline parameters for one sample
rule make_input_yml:
  input: 
    reads = lambda wildcards: config["read_files"][wildcards.sample],
   # reference = config["reference"]
  output: "results/{sample}/input.yml"
  params:
    cell_numbers = lambda wildcards: config["cell_numbers"][wildcards.sample],
    generate_bam = True,
    threads = config["threads"]
  conda: "envs/run_workflow.yml"
  script:
    "scripts/make_input_yml.R"

# run BD Rhapsody pipeline
rule run_rhapsody:
  input: "results/{sample}/input.yml"
  output: 
    seurat = "results/{sample}/Seurat.rds",
    report = "results/{sample}/Pipeline_Report.html"
  params:
    outdir = "results/{sample}"
  conda: "envs/run_workflow.yml"
  threads: config["threads"]
  resources:
    mem_mb = "128000",
    runtime = "48h"
  shell:
    "rhapsody pipeline --outdir {params.outdir} {input}"
